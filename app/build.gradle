plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
}

def appName = "es.upsa.mimo.gamercollection"

def versionMajor = 2
def versionMinor = 4
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

android {

    namespace appName
    compileSdk 34

    signingConfigs {
        release {
            if (project.hasProperty('GAMER_COLLECTION_STORE_FILE')) {
                storeFile file(GAMER_COLLECTION_STORE_FILE)
                storePassword GAMER_COLLECTION_STORE_PASSWORD
                keyAlias GAMER_COLLECTION_KEY_ALIAS
                keyPassword GAMER_COLLECTION_KEY_PASSWORD
            }
        }
    }

    defaultConfig {

        applicationId appName
        minSdkVersion 23
        targetSdkVersion 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        versionCode versionMajor * 100000 + versionMinor * 1000 + versionPatch * 10 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "@string/app_name"]
        }
        //This is not necessary because debug type is always include in the project by default,
        //but I include it because I want to set the suffix
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '.debug'
            manifestPlaceholders = [appName: "Gamer Collection - Pre"]
        }
    }

    buildFeatures {
        buildConfig = true
        dataBinding = true
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    kapt {
        correctErrorTypes = true
    }
}

dependencies {

    //Android
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.fragment:fragment-ktx:1.8.8'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    //MVVM & LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"

    //Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    //Google Play Services
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'com.google.android.gms:play-services-maps:19.2.0'

    //Gson
    implementation 'com.google.code.gson:gson:2.13.1'

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0'

    //Picasso
    implementation 'com.squareup.picasso:picasso:2.8'

    //Material RatingBar
    implementation 'me.zhanghai.android.materialratingbar:library:1.4.0'

    //BlurImageView
    implementation 'com.github.jgabrielfreitas:BlurImageView:1.0.1'

    //Room
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"

    //Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'

    //Security
    implementation 'androidx.security:security-crypto:1.0.0'

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:33.16.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-config-ktx'

    //Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    //Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}
